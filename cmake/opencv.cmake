# Try to find system OpenCV first
find_package(OpenCV QUIET)
if(OpenCV_FOUND)
    message("[Lite.AI.Toolkit][I] Found system OpenCV: ${OpenCV_VERSION}")
    set(OpenCV_Version ${OpenCV_VERSION} CACHE STRING "OpenCV version" FORCE)
    include_directories(${OpenCV_INCLUDE_DIRS})
else()
    set(OpenCV_Version "4.9.0-ffmpeg4.2.2" CACHE STRING "OpenCV version" FORCE)
    set(OpenCV_DIR ${THIRD_PARTY_PATH}/opencv)
    # download from github if opencv library is not exists
    if (NOT EXISTS ${OpenCV_DIR})
        set(OpenCV_Filename "opencv.tar.gz")
        set(OpenCV_URL https://ghfast.top/https://github.com/xlite-dev/lite.ai.toolkit/releases/download/v0.0.1/opencv.tar.gz)
        message("[Lite.AI.Toolkit][I] Downloading library: ${OpenCV_URL}")
        download_and_decompress(${OpenCV_URL} ${OpenCV_Filename} ${OpenCV_DIR}) 
        create_ffmpeg_syslinks_if_not_found(${OpenCV_DIR}/lib)
    else() 
        message("[Lite.AI.Toolkit][I] Found local OpenCV library: ${OpenCV_DIR}")
    endif() 
    if(NOT EXISTS ${OpenCV_DIR})
        message(FATAL_ERROR "[Lite.AI.Toolkit][E] ${OpenCV_DIR} is not exists!")
    endif()

    include_directories(${OpenCV_DIR}/include/opencv4)
    link_directories(${OpenCV_DIR}/lib)
endif()

if(NOT WIN32)
    if(NOT OpenCV_FOUND)
        if(ENABLE_OPENCV_VIDEOIO OR ENABLE_TEST)
            set(OpenCV_LIBS opencv_core opencv_imgproc opencv_imgcodecs opencv_video opencv_videoio opencv_calib3d)
        else()
            set(OpenCV_LIBS opencv_core opencv_imgproc opencv_imgcodecs opencv_calib3d) # no videoio, video module
        endif()
    else()
        # System OpenCV - use libraries from find_package
        set(OpenCV_LIBS ${OpenCV_LIBS})
    endif()
else()
    set(OpenCV_LIBS opencv_world490)
endif()
